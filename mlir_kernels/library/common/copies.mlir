// Common copy functions for AMDGCN kernels.

// Drive this through pytest, only check input IR validity here.
// RUN: cat %s \
// RUN: | aster-opt --amdgcn-preload-library="library-paths=%p/library/common/register_init.mlir,%p/library/common/indexing.mlir" \
// RUN: | FileCheck %s

!s   = !amdgcn.sgpr
!sx2 = !amdgcn.sgpr_range<[? + 2]>
!sx4 = !amdgcn.sgpr_range<[? + 4]>

!v   = !amdgcn.vgpr
!vx2 = !amdgcn.vgpr_range<[? + 2]>
!vx4 = !amdgcn.vgpr_range<[? + 4]>

!a   = !amdgcn.agpr
!ax2 = !amdgcn.agpr_range<[? + 2]>
!ax4 = !amdgcn.agpr_range<[? + 4]>

amdgcn.library @common_copies isa = [#amdgcn.isa<cdna3>] {
  //===--------------------------------------------------------------------===//
  // Library function declarations (provided by amdgcn-preload-library pass)
  //===--------------------------------------------------------------------===//
  // register_init.mlir
  func.func private @alloc_vgprx2() -> !vx2
  func.func private @init_vgprx4(i32) -> !vx4
  // indexing.mlir
  func.func private @lane_delinearize_2d(index, index) -> (index, index)
  func.func private @matrix_offset(index, index, index, index) -> !v
  func.func private @tiled_matrix_offset(index, index, index, index, index, index) -> !v
  func.func private @tiledx2_matrix_offset(index, index, index, index, index, index, index, index) -> !v
  func.func private @swizzle_A_16x16xf16() -> (index, index)
  func.func private @swizzle_C_16x16xf32() -> (index, index)

  //===--------------------------------------------------------------------===//
  // Global <-> LDS
  //===--------------------------------------------------------------------===//
  // Load a 16x16xdword2 tile from global memory to LDS within a single wave, in
  // a **synchronized fashion** (i.e. waitcnt 0 are inserted after global_load
  // and after lds_write).
  // The implementation assumes access to be performed in a tiled fashion, where
  // there is a major tile and a minor tile within it. The caller is
  // responsible for embedding distribution information into the indices.
  // The callee is responsible for computing the offsets within the tiles based on
  // the lane id.

  func.func private @global_load_to_lds_wave_16x16_dwordx2_wait(
    %ptr: !sx2,           // The global base pointer
    %lds_base_off: index, // The local base offset in LDS
    %m_pos: index,        // The outer-most major-tile position
    %n_pos: index,        // The inner-most major-tile position
    %N_SIZE: index,       // The inner-most size
    %mm_pos: index,       // The outer-most minor-tile position
    %nn_pos: index,       // The inner-most minor-tile position
    %NN_SIZE: index       // The inner-most major-tile size
  ) {
    // Constants
    %c4 = arith.constant 4 : index
    %c16 = arith.constant 16 : index
    %elt_size = arith.constant 2 : index // f16 size in bytes

    // Get local positions within the minor tile
    %mmm, %nnn = func.call @lane_delinearize_2d(%c16, %c4)
      : (index, index) -> (index, index)
    %nnn_pos = affine.apply affine_map<()[nnn] -> (4 * nnn)>()[%nnn]

    // Calculate global offset
    %off_reg = func.call @tiledx2_matrix_offset(
      %m_pos, %n_pos, %mm_pos, %nn_pos, %mmm, %nnn_pos, %N_SIZE, %elt_size)
      : (index, index, index, index, index, index, index, index) -> !v

    // Perform the load
    %dst = func.call @alloc_vgprx2() : () -> (!vx2)
    %loaded = amdgcn.flat.global_load <global_load_dwordx2> %dst, %ptr[%off_reg]
      : !vx2, !sx2[!v] -> !vx2

    // Wait for load completion
    amdgcn.sopp.s_waitcnt #amdgcn.inst<s_waitcnt> vmcnt = 0

    // Calculate offset into LDS
    %off_lds_reg = func.call @tiled_matrix_offset(%mm_pos, %nn_pos, %mmm, %nnn_pos, %NN_SIZE, %elt_size)
      : (index, index, index, index, index, index) -> !v

    // DS write to LDS
    %l_off_i32 = arith.index_cast %lds_base_off : index to i32
    amdgcn.ds.write #amdgcn.inst<ds_write_b64> %loaded, %off_lds_reg, offset = %l_off_i32
      : !vx2, !v, i32

    // Wait for LDS write
    amdgcn.sopp.s_waitcnt #amdgcn.inst<s_waitcnt> lgkmcnt = 0

    return
  }

  // Loads from global memory to VGPRs, in a **synchronized fashion** (i.e.
  // waitcnt 0 are inserted after global_load).
  // This function cooperatively loads 64 dwordx2 (i.e. 256 (16x16) f16 elements),
  // depending on %num_rows:
  //   - when %num_rows =  1, a 1x64xdwordx2 tile is loaded from global memory to VGPRs
  //   - when %num_rows =  2, a 2x32xdwordx2 tile is loaded from global memory to VGPRs
  //   - when %num_rows =  4, a 4x16xdwordx2 tile is loaded from global memory to VGPRs
  //   - when %num_rows =  8, a  8x8xdwordx2 tile is loaded from global memory to VGPRs
  //   - when %num_rows = 16, a 16x4xdwordx2 tile is loaded from global memory to VGPRs
  //   - when %num_rows = 32, a 32x2xdwordx2 tile is loaded from global memory to VGPRs
  //   - when %num_rows = 64, a 64x1xdwordx2 tile is loaded from global memory to VGPRs
  // This can be configured for better global memory coalescing when %num_rows is not 1.
  // This is typically useful when %N_SIZE is 64 (or greater), (resp. 32, 16, 8, 4, 2, 1).
  // We use an extra %num_rows (instead of just %N_SIZE) to give the caller the
  // option to use non-coalesced loads and obtain better flexibility (e.g. useful
  // for pipelining)
  //
  // TODO: also add a variant with upper bounds and buffer_load to handle boundary conditions.
  // TODO: add a static assert to enforce these.
  func.func private @global_load_wave_64xdwordx2_wait(
    %ptr: !sx2,           // The global base pointer
    %m_pos: index,        // The outer-most major-tile position
    %n_pos: index,        // The inner-most major-tile position
    %N_SIZE: index,       // The inner-most size
    %mm_pos: index,       // The outer-most minor-tile position
    %nn_pos: index,       // The inner-most minor-tile position
    %num_rows: index      // The number of rows in the 256 elements
  ) -> !vx2 {
    // Constants
    %elt_size = arith.constant 2 : index // f16 size in bytes
    %wave_size = arith.constant 64 : index

    %num_cols = affine.apply affine_map<()[wave_size, num_rows]
      -> (wave_size ceildiv num_rows)>()[%wave_size, %num_rows]

    // Get local positions within the minor tile
    %mmm_pos, %nnn = func.call @lane_delinearize_2d(%num_rows, %num_cols) : (index, index) -> (index, index)
    %nnn_pos = affine.apply affine_map<()[nnn] -> (4 * nnn)>()[%nnn]

    // Calculate global offset
    %off_reg = func.call @tiledx2_matrix_offset(
      %m_pos, %n_pos, %mm_pos, %nn_pos, %mmm_pos, %nnn_pos, %N_SIZE, %elt_size)
      : (index, index, index, index, index, index, index, index) -> !v

    // Perform the load
    %dst = func.call @alloc_vgprx2() : () -> (!vx2)
    %loaded = amdgcn.flat.global_load <global_load_dwordx2> %dst, %ptr[%off_reg]
      : !vx2, !sx2[!v] -> !vx2

    // Wait for load completion
    amdgcn.sopp.s_waitcnt #amdgcn.inst<s_waitcnt> vmcnt = 0

    return %loaded : !vx2
  }

  // Write %value to LDS.
  // This function cooperatively writes 64 dwordx2 (i.e. 256 (16x16) f16 elements),
  // depending on %num_rows:
  //   - when %num_rows =  1, a 1x64xdwordx2 tile is written from VGPRs to LDS
  //   - when %num_rows =  2, a 2x32xdwordx2 tile is written from VGPRs to LDS
  //   - when %num_rows =  4, a 4x16xdwordx2 tile is written from VGPRs to LDS
  //   - when %num_rows =  8, a  8x8xdwordx2 tile is written from VGPRs to LDS
  //   - when %num_rows = 16, a 16x4xdwordx2 tile is written from VGPRs to LDS
  //   - when %num_rows = 32, a 32x2xdwordx2 tile is written from VGPRs to LDS
  //   - when %num_rows = 64, a 64x1xdwordx2 tile is written from VGPRs to LDS
  // This can be configured to match the memory coalescing needs needs of a producer
  // global_load_wave_64xdwordx2_waitglobal when %num_rows is not 1.
  // This is typically useful when %N_SIZE is 64 (or greater), (resp. 32, 16, 8, 4, 2, 1).
  // We use an extra %num_rows (instead of just %N_SIZE) to give the caller the
  // option to use non-coalesced loads and obtain better flexibility (e.g. useful
  // for pipelining)
  //
  // TODO: also add a variant with upper bounds and buffer_load to handle boundary conditions.
  // TODO: add a static assert to enforce these.
  func.func private @lds_write_wave_64xdwordx2_wait(
    %lds_base_off: index, // The local base offset in LDS
    %mm_pos: index,       // The outer-most minor-tile position
    %nn_pos: index,       // The inner-most minor-tile position
    %N_SIZE: index,       // The inner-most major-tile size
    %num_rows: index,     // The number of rows in the 256 elements
    %value: !vx2          // The value to write to LDS
  ) {
    // Constants
    %elt_size = arith.constant 2 : index // f16 size in bytes
    %wave_size = arith.constant 64 : index

    %num_cols = affine.apply affine_map<()[wave_size, num_rows]
      -> (wave_size ceildiv num_rows)>()[%wave_size, %num_rows]

    // Get local positions within the minor tile
    %mmm_pos, %nnn = func.call @lane_delinearize_2d(%num_rows, %num_cols) : (index, index) -> (index, index)
    %nnn_pos = affine.apply affine_map<()[nnn] -> (4 * nnn)>()[%nnn]

    // Calculate offset into LDS
    %off_lds_reg = func.call @tiled_matrix_offset(
        %mm_pos, %nn_pos, %mmm_pos, %nnn_pos, %N_SIZE, %elt_size)
      : (index, index, index, index, index, index) -> !v

    // DS write to LDS
    %l_off_i32 = arith.index_cast %lds_base_off : index to i32
    amdgcn.ds.write #amdgcn.inst<ds_write_b64> %value, %off_lds_reg, offset = %l_off_i32
      : !vx2, !v, i32

    // Wait for LDS write
    amdgcn.sopp.s_waitcnt #amdgcn.inst<s_waitcnt> lgkmcnt = 0

    return
  }

  // Store a dword to global memory, in a **synchronized fashion** (i.e.
  // waitcnt 0 are inserted after global_store).
  // The caller is responsible for embedding distribution information into the
  // positions %m_pos and %n_pos (and make them workgroup/wave/thread/lane-dependent).
  func.func private @store_to_global_dword_wait(
    %value: !v,     // Value to store
    %ptr: !sx2,     // The global base pointer
    %m_pos: index,  // The outer-most position
    %n_pos: index,  // The inner-most position
    %N_SIZE: index  // The inner-most size (stride)
  ) {
    %elt_size = arith.constant 4 : index // dword size in bytes
    %off_reg = func.call @matrix_offset(%m_pos, %n_pos, %N_SIZE, %elt_size)
      : (index, index, index, index) -> !v
    amdgcn.flat.global_store <global_store_dword> %value, %ptr[%off_reg] : !v, !sx2[!v]
    amdgcn.sopp.s_waitcnt #amdgcn.inst<s_waitcnt> vmcnt = 0
    return
  }

  //===--------------------------------------------------------------------===//
  // Swizzled fragment reads/writes
  //===--------------------------------------------------------------------===//
  // Read the `A` fragment (16x16xf16) from LDS to VGPRs, in a **synchronized
  // fashion** (i.e. waitcnt 0 is inserted after the ds_read).
  // The caller is responsible for embedding distribution information into the
  // positions %m_pos and %n_pos.
  func.func private @lds_read_A_wave_16x16xf16_fragment_wait(
    %lds_base: index, // The local base offset in LDS
    %m_pos: index,    // The outer-most tile position
    %n_pos: index,    // The inner-most tile position
    %N_SIZE: index    // The inner-most size
  ) -> !vx2 {
    // Compute the swizzled positions
    %elt_size = arith.constant 2 : index // f16 size in bytes
    %mm_pos, %nn_pos = func.call @swizzle_A_16x16xf16() : () -> (index, index)
    %off_lds_reg = func.call @tiled_matrix_offset(%m_pos, %n_pos, %mm_pos, %nn_pos, %N_SIZE, %elt_size)
      : (index, index, index, index, index, index) -> !v

    // Perform the DS read
    %lds_base_i32 = arith.index_cast %lds_base : index to i32
    %dst = func.call @alloc_vgprx2() : () -> (!vx2)
    %from_lds = amdgcn.ds.read #amdgcn.inst<ds_read_b64> %dst, %off_lds_reg, offset = %lds_base_i32
      : !v, i32 -> !vx2

    amdgcn.sopp.s_waitcnt #amdgcn.inst<s_waitcnt> lgkmcnt = 0
    return %from_lds : !vx2
  }

  // Store the `C` fragment (16x16xf32) from VGPRs to global memory, in a
  // **synchronized fashion** (i.e. waitcnt 0 is inserted after each global_store).
  // The caller is responsible for embedding distribution information into the
  // positions. The callee computes and embeds the swizzled positions.
  // This function assumes a major/minor tile structure for the global positions.
  func.func private @global_store_wave_16x16xf32_swizzled_C_fragment_wait(
    %acc: !vx4,           // The accumulator fragment to store
    %ptr: !sx2,           // The global base pointer
    %m_pos: index,        // The outer-most major-tile position
    %n_pos: index,        // The inner-most major-tile position
    %N_SIZE: index,       // The inner-most size
    %mm_pos: index,       // The outer-most minor-tile position
    %nn_pos: index        // The inner-most minor-tile position
  ) {
    // Constants
    %c0 = arith.constant 0 : index
    %c1 = arith.constant 1 : index
    %c2 = arith.constant 2 : index
    %c3 = arith.constant 3 : index
    %c4 = arith.constant 4 : index

    // Split the fragment into 4 dword values
    %v0, %v1, %v2, %v3 = amdgcn.split_register_range %acc : !vx4
    %C_fragment =  memref.alloca() : memref<4x!v>
    memref.store %v0, %C_fragment[%c0] : memref<4x!v>
    memref.store %v1, %C_fragment[%c1] : memref<4x!v>
    memref.store %v2, %C_fragment[%c2] : memref<4x!v>
    memref.store %v3, %C_fragment[%c3] : memref<4x!v>

    // Compute the swizzled positions
    %mmm_pos, %nnn_pos = func.call @swizzle_C_16x16xf32() : () -> (index, index)

    // Calculate global j position
    %n_global_pos = affine.apply
      affine_map<()[n_pos, nn_pos, nnn_pos] -> (n_pos + nn_pos + nnn_pos)>
      ()[%n_pos, %nn_pos, %nnn_pos]

    // Store each fragment to global memory
    scf.for %mmmm_pos = %c0 to %c4 step %c1 {
      %fragment = memref.load %C_fragment[%mmmm_pos] : memref<4x!v>
      // Calculate global i position
      %m_global_pos = affine.apply
        affine_map<()[m_pos, mm_pos, mmm_pos, mmmm_pos] -> (m_pos + mm_pos + mmm_pos + mmmm_pos)>
        ()[%m_pos, %mm_pos, %mmm_pos, %mmmm_pos]

      // Store to global memory with wait
      func.call @store_to_global_dword_wait(%fragment, %ptr, %m_global_pos, %n_global_pos, %N_SIZE)
        : (!v, !sx2, index, index, index) -> ()
    } {amdgcn.constexpr}
    return
  }
}
